// EOL supports unique, ordered set
var nodes = Graph.all.first.nodes.asOrderedSet;

"Original:".println;
"Name, Value, Group".println;
for (node in nodes){
	Sequence{node.name, node.value , node.group}.concat(", ").println;
}

"\nSort By Name:".println;
"Name, Value, Group".println;
nodes = nodes.sortBy(n | n.name);
for (node in nodes){
	Sequence{node.name, node.value , node.group}.concat(", ").println;
}

"\nSort By Value:".println;
"Name, Value, Group".println;
nodes = nodes.sortBy(n | n.value);
for (node in nodes){
	Sequence{node.name, node.value , node.group}.concat(", ").println;
}

"\nSort By Group, Name:".println;
"Name, Value, Group".println;
nodes = nodes.sortBy(n | n.group + "-" + n.name);
for (node in nodes){
	Sequence{node.name, node.value , node.group}.concat(", ").println;
}

// However, for this one, values are treated as strings not as numbers
"\nSort By Group, Value:".println;
"Name, Value, Group".println;
nodes = nodes.sortBy(n |  n.group + "-" + n.value);
for (node in nodes){
	Sequence{node.name, node.value , node.group}.concat(", ").println;
}
// So, we should perform multi level sorting based on the order of the groups
// and add the elements one by one to an empty OrderedSet/Sequence.

"\nMultilevel Sort By Group, Value, Name:".println;
"Name, Value, Group".println;
nodes = nodes.sortBy(Sequence{"group", "value", "name"});
for (node in nodes){
	Sequence{node.name, node.value , node.group}.concat(", ").println;
}

"\nMultilevel Sort By Group, Name, Value:".println;
"Name, Value, Group".println;
nodes = nodes.sortBy(Sequence{"group", "name", "value"});
for (node in nodes){
	Sequence{node.name, node.value , node.group}.concat(", ").println;
}

operation Collection sortBy(featuresNames : Sequence(String)): Collection {
	if (featuresNames.size == 0) {
		return self; 
	}
	var sequence = Sequence{};
	var featureName = featuresNames.get(0);
	featuresNames.removeAt(0);
	var sortedSelf = self.sortBy(n | n.eGet(n.eClass.getEStructuralFeature(featureName)));
	if (featuresNames.size > 0) {
		// we collect the unique members of the feature
		var members = sortedSelf.collect(n | n.eGet(n.eClass.getEStructuralFeature(featureName))).asOrderedSet;
		// iterate through all the members
		for (member in members){
			// create a sublist by filtering the collection for each member
			var sublist = sortedSelf.select(n | n.eGet(n.eClass.getEStructuralFeature(featureName)) == member);
			var clonedFeatureNames = featuresNames.clone();
			// we do a recursive call here to sort the sublist
			var sortedSublist = sublist.sortBy(clonedFeatureNames);
			sequence.addAll(sortedSublist);
		}
	} else {
		return sortedSelf;
	}
	return sequence;
}

